set windows-shell := ["cmd", "/c"]

_default:
    @just --list

asm := join(justfile_directory(), "src", "main.asm")
ld := join(justfile_directory(), "src", "setup.ld")

elf := join(justfile_directory(), "target", "debug", "main.elf")
img := join(justfile_directory(), "target", "debug", "main.img")
lst := join(justfile_directory(), "target", "debug", "main.lst")
obj := join(justfile_directory(), "target", "debug", "main.obj")

export LD := env("LD", "ld")
export OBJCOPY := env("OBJCOPY", "objcopy")

assemble:
    mkdir {{ if os_family() == "unix" {"-p"} else {""} }} {{ parent_directory(img) }}
    nasm -Wall -f elf -g -l {{ lst }} -o {{ obj }} {{ asm }}
    {{ LD }} --script {{ ld }} -o {{ elf }} {{ obj }}
    {{ OBJCOPY }} --output-target binary {{ elf }} {{ img }}

[unix]
deploy:
    #!/bin/sh
    set -euo pipefail
    drives=$(lsblk --nodeps --output "name,size,model" --paths --scsi | tail --lines +2)
    choice=$(echo "${drives}" | gum choose --header "Select SCSI device" | cut --delimiter " " --fields 1)
    gum confirm --default=false "Format ${choice}" || exit $?
    sudo umount ${choice}* 2>/dev/null || true
    sudo dd bs=4MB conv=fsync if={{ img }} of=${choice} status=progress

gdbinit := join(justfile_directory(), "src", "gdbinit_real_mode.txt")
tdesc := join(justfile_directory(), "src", "target.xml")

# @see: https://github.com/mhugo/gdb_init_real_mode/blob/master/gdbinit_real_mode.txt
# @see: https://github.com/qemu/qemu/blob/v7.2.0/gdb-xml/i386-32bit.xml
gdb:
    gdb {{ elf }} \
        --init-command {{ gdbinit }} \
        --eval-command "set tdesc filename {{ tdesc }}" \
        --eval-command "target remote 127.0.0.1:1234" \
        --eval-command "break _start" \
        --eval-command "continue"

qemu *flags:
    qemu-system-i386 -drive file={{ img }},format=raw -net none {{ flags }}
